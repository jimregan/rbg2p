CHARACTER_SET "ΐάέήίΰαβγδεζηθικλμνξοπρςστυφχψωϊϋόύώabcdefghijklmnopqrstuvwxyzé"
//PHONEME_SET|IPA "a b c d e f g i k l m n o p r s t u v x z ç ð ɟ ɣ ɲ ʎ ʝ ˈ ˌ θ d⁀z t⁀s k⁀s p⁀s"
PHONEME_SET "a e i o u af ef if av ev iv r p b t d k g f v s z x G m n l T D J C jj j gj c ts dz ks ps ""

DEFAULT_PHONEME "_"
PHONEME_DELIMITER " "

VAR PALAT (οι|οί|αι|αί|η|ή|ι|ί|ύ|υ|ε|έ)
VAR PALATLAT (oi|oí|ai|aí|i|e|í|é)

VAR VOWEL [ΐάέήίΰαεηιουωϊϋόύώ]
VAR CONS [βγδζθκλμνξπρςστφχψ]

VAR VOICED [ΐάέήίΰαβγεζηιλμνορυωϊϋόύώ]
VAR UNVOICED [δθκξπςστφχψ]

//FILTER "n n ([gk])" -> "n $1"


// Latin
bb -> b
dd -> d
ff -> f
gg -> g
hh -> x
jj -> j
kk -> k
ck -> k
ll -> l
mm -> m
nn -> n
pp -> p
rr -> r
ss -> s
tt -> t
vv -> v
ou -> u
oo -> u
th -> T
ch -> C / _ PALATLAT
ch -> x
//nd -> (n d, d)
//mp -> (m b, b)

ai -> e
ei -> i
oi -> i
iι -> i
oι -> o

a -> a
e -> e
é -> " e
y -> i
u -> u
i -> i
o -> o
b -> b
c -> s / _ PALATLAT
c -> k
d -> D
f -> f
g -> G
h -> x
j -> j
k -> k
l -> l
m -> m
n -> n
p -> p
q -> k
r -> r
s -> s
t -> t
v -> v
w -> v
x -> ks
z -> z


// Quadruple character rules
λλιά -> jj " a
λλια -> jj a

// Quadruple character tests
αλλιά -> a jj " a
αλλια -> a jj a


// ??
//νιού -> J " u
//νεια - > J a
//νειά -> J " a
//νειo -> J o
//νειό -> J " o



// Triple character rules

// ???
// ιου -> u

λια -> jj a
λιά -> jj " a
λιο -> jj o
λιό -> jj " o
λιω -> jj o
λιώ -> jj " o

κκι -> c i
κκί -> c " i

κια -> c a
κιά -> c " a
κιού -> c " u
κιο -> c o
κιό -> c " o
κιω -> c o
κιώ -> c " o

νιω -> J o
νιώ -> J " o
νια -> J a
νιά -> J " a
//νιού -> J " u // HL 20171020? NL: Don't think so. Sometimes n i " u
//νιου -> J u // HL 20171020? NL: Don't think so. Sometimes n i u
//νιο -> J o // HL 20171020? NL: Don't think so. Sometimes n i o
//νιό -> J " o // HL 20171020?

//χια -> C a // HL 20171020?
//χιά -> C " a // HL 20171020?
//χιο -> C o // HL 20171020?
//χιό -> C " o // HL 20171020?
//χιω -> C o // HL 20171020?
//χιώ -> C " o // HL 20171020?

// [j] före [e] eller [i]; annars [G]
για -> j a
γιά -> j " a
γαι -> j e
γεί -> j " i
γει -> j i
γοί -> j " i
γοι -> j i
γύι -> j " i
γυι -> j i
γι -> (j i, j) / _ VOWEL
γι -> j i
γε -> (j e, j) / _ VOWEL
γε -> j e


μπτ -> (m p t , m t)


// Triple character tests
TEST αγια -> a j a
TEST λιον -> jj o n
TEST αλιό -> a jj " o
TEST λιαν -> jj a n
TEST κοκκι -> k o c i
TEST οκκίν -> o c " i n
TEST ικιαν -> i c a n
TEST κιοπ -> c o p
TEST ακιω -> a c o
TEST ίνιων -> " i J o n
TEST νιας -> J a s
TEST ακιό -> a c " o
TEST ινιάς -> i J " a s
TEST γιάν -> j " a n
TEST ογια -> o j a

TEST αμπτ -> (a m p t, a m t)

// dipththongs (not mapped to IPA)
αυ -> av / _ VOICED
αυ -> af / _ UNVOICED
αύ -> " av / _ VOICED
αύ -> " af / _ UNVOICED
ευ -> ev / _ VOICED
ευ -> ef / _ UNVOICED
εύ -> " ev / _ VOICED
εύ -> " ef / _ UNVOICED
ηυ -> if / _ UNVOICED
ηυ -> iv / _ VOICED
ηύ -> " if / _ UNVOICED
ηύ -> " iv / _ VOICED

// ???
//υο -> iv / _ VOICED
//υο -> if / _ UNVOICED
//υο -> " iv / _ VOICED
//υο -> " if / _ UNVOICED


// Double character rules

// TODO rules for ιο ?
// TODO rules for σι ?
// TODO rules for στι ?

αί -> " e
αι -> e
εί -> " i
ει -> i
οί -> " i
οι -> i
ου -> u
ού -> " u
υι -> i
ύι -> " i
ωι -> o
σσ -> s
μπ -> b / # _
μπ -> (m b, b)
ρρ -> r
λλ -> l
νν -> n
ππ -> p
ββ -> v
κκ -> k
φφ -> f
μμ -> m
ττ -> t
χχ -> C / _ PALAT
χχ -> x


// ??
//γγ -> n gj /  _ PALAT //? TODO: kolla upp?
γγ -> n g

γκ -> gj / # _ PALAT
γκ -> n gj /  _ PALAT
γκ -> g / # _
γκ -> n g

γχ -> n C / _ PALAT
γχ -> n x

ντζ -> (n dz, dz)
ντσ -> (n ts, n dz)
ντς -> n ts / _ #
ντ -> d / # _
ντ -> (n d, d)

τζ -> dz
τσ -> ts
τς -> ts / _ #


// Double character tests
TEST μαί -> m " e
TEST δαι -> D e
TEST ρεί -> r " i
TEST λει -> l i
TEST φου -> f u
TEST πού -> p " u
TEST ποί -> p " i
TEST τοι -> t i
TEST ουμπου -> (u m b u, u b u)
TEST μπου -> b u
TEST αρρη -> a r i
TEST υλλο -> i l o
TEST αννό -> a n " o
TEST σσης -> s i s
TEST αγκο -> a n g o
TEST νταλ -> d a l
TEST οντο -> (o n d o, o d o)
TEST ρυι -> r i
TEST τζα -> dz a
TEST τζη -> dz i
TEST τση -> ts i
//TEST ογγυ -> o n gj i // ?
TEST αγγα -> a n g a
TEST αυκ -> af k
TEST αυρ -> av r
TEST πευτ -> p ef t
TEST ρευμ -> r ev m
TEST γχι -> n C i
TEST γχου -> n x u

//
// Default single character rules
//

ΐ -> " i
ά -> " a
έ ->  " e
ή -> " i
ί -> " i
ΰ -> " i
α -> a
β -> v
γ -> j / _ PALAT
γ -> G
δ -> D
ε -> e
ζ -> z
η -> i
θ -> T
ι -> i
κ -> c / _ PALAT
κ -> k
λ -> l
μ -> m
ν -> n
ξ -> ks
ο -> o
π -> p
ρ -> r
ς -> s
// Voicing of σ before some voiced consonants
σ -> z / _ [βγδμνρ]
σ -> s
τ -> t
υ -> i
φ -> f
χ -> C / _ PALAT
χ -> x
ψ -> ps
ω -> o
ϊ -> i
ϋ -> i
ό -> " o
ύ -> " i
ώ -> " o

// Default single character rule tests
TEST αΐ -> a " i
TEST ρά -> r " a
TEST λέ -> l " e
TEST ιή -> i " i
TEST ρί -> r " i
TEST αΰ -> a " i
TEST πα -> p a
TEST βα -> v a
TEST γη -> j i
TEST ηγ -> i G
TEST δε -> D e
TEST με -> m e
TEST ζα -> z a
TEST νη -> n i
TEST θε -> T e
TEST ρι -> r i
TEST κω -> k o
TEST αλ -> a l
TEST μα -> m a
TEST να -> n a
TEST ξη -> ks i
TEST ρο -> r o
TEST πα -> p a
TEST ρα -> r a
TEST ης -> i s
TEST σβήνω -> z v " i n o
TEST σι -> s i
TEST τε -> t e
TEST ρυ -> r i
TEST φυ ->  f i
TEST χυ -> C i
TEST χα -> x a
TEST ψη -> ps i
TEST κι -> c i
TEST κω -> k o
TEST αϊ -> a i
TEST εϋ -> e i
TEST πό -> p " o
TEST ρύ -> r " i
TEST χώ -> x " o
TEST λεύκτρου -> l " ef k t r u
TEST εντζ -> (e n dz, e dz)
TEST ψιά -> ps i " a
TEST ογιού -> (o j i " u, o j " u)
TEST για -> j a
TEST γιά -> j " a
TEST γαι -> j e
TEST γεί -> j " i
TEST γει -> j i
TEST γοί -> j " i
TEST γοι -> j i
TEST γύι -> j " i
TEST γυι -> j i
//TEST ντονγκ -> d o n g
TEST ντονγκ -> d o n n g
TEST γαϊδούρι -> G a i D " u r i // FORVO
TEST σμάραγδος -> z m " a r a G D o s // FORVO
TEST τηγάνια -> t i G " a J a // ?
TEST τηγανιά -> t i G a J " a // ?
TEST Γωνιακό -> G o J a k " o // ?
TEST πεντικιούρ -> (p e n d i c " u r, p e d i c " u r)
